reference 'Microsoft.ProgramSynthesis.Extraction.Text.Semantics.dll';
reference 'ProseSample.Substrings.dll';
reference 'Microsoft.CodeAnalysis.CSharp.dll';
reference 'Microsoft.CodeAnalysis.dll';

using Microsoft.ProgramSynthesis.Extraction.Text.Semantics;
using ProseSample.Substrings;
using System.Collections.Generic;
using Microsoft.CodeAnalysis.CSharp;
using Microsoft.CodeAnalysis;
using System;

using semantics ProseSample.Substrings.Semantics;
using learners ProseSample.Substrings.WitnessFunctions;

language ProseSample_Match;

@vardefault[VariableScore]
feature double Score = RankingScore;

@start IEnumerable<SyntaxNodeOrToken> spans := NodesMap(script, nodes) = Map(\node: SyntaxNodeOrToken => script, nodes);

IEnumerable<SyntaxNodeOrToken> nodes := SplitNodes(n);

SyntaxNodeOrToken script := Script(node, trans);

SyntaxNodeOrToken edit := Insert(node, k, parent, ast) | Update(node, from, to) | Move(node, k, parent, ast) | Delete(node, from);
MatchResult parent := match;
MatchResult from := match;
SyntaxNodeOrToken to := ast;
IEnumerable<SyntaxNodeOrToken> trans := SE(edit) | EList(edit, trans);

MatchResult match := C(node, kind, children) | Literal(node, tree) | Abstract(node, kind, k);
MatchResult child := match;
IEnumerable<MatchResult> children := SC(child) | CList(child, children);

SyntaxNodeOrToken ast:= Node(kind, childrenNodes) | Const(tree);
IEnumerable<SyntaxNodeOrToken> childrenNodes := SN(ast) | NList(ast, childrenNodes);

@feature[Score=KindScore] SyntaxKind kind;                     @input SyntaxNodeOrToken n;
@feature[Score=KDScore] string id;                             @feature[Score=KScore] int k;
@feature[Score=NodeScore] SyntaxNodeOrToken tree;
