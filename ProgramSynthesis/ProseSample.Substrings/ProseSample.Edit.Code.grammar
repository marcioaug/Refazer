reference 'Microsoft.ProgramSynthesis.Extraction.Text.Semantics.dll';
reference 'ProseSample.Substrings.dll';
reference 'Microsoft.CodeAnalysis.CSharp.dll';
reference 'Microsoft.CodeAnalysis.dll';
reference 'Tutor.exe';

using Microsoft.ProgramSynthesis.Extraction.Text.Semantics;
using ProseSample.Substrings;
using System.Collections.Generic;
using Microsoft.CodeAnalysis.CSharp;
using Microsoft.CodeAnalysis;
using Tutor.Spg.Node;
using System;

using semantics ProseSample.Substrings.Semantics;
using learners ProseSample.Substrings.WitnessFunctions;

language ProseSample_Match;

@vardefault[VariableScore]
feature double Score = RankingScore;

@start IEnumerable<SyntaxNodeOrToken> spans := NodesMap(trans, nodes) = Map(\node: SyntaxNodeOrToken => trans, nodes);

IEnumerable<SyntaxNodeOrToken> nodes := Stts(pred, splits) = Filter(\x: SyntaxNodeOrToken => pred, splits);
Boolean pred := FTrue();
IEnumerable<SyntaxNodeOrToken> splits := SplitNodes(n);

SyntaxNodeOrToken trans := ManyTrans(node, loop);

IEnumerable<SyntaxNodeOrToken> loop := Loop(script, breaks) = Map(\node: SyntaxNodeOrToken => script, breaks);

IEnumerable<SyntaxNodeOrToken> breaks := Breaks(pred, regions) = Filter(\x: SyntaxNodeOrToken => pred, regions);

IEnumerable<SyntaxNodeOrToken> regions := Template(node, template);

Pattern template:= P(kind, pchildren) | Abstract(kind) | Concrete(tree);
IEnumerable<Pattern> pchildren := SP(template) | PList(template, pchildren);

SyntaxNodeOrToken script := Script(node, edits);

IEnumerable<SyntaxNodeOrToken> edits := SE(edit) | EList(edit, edits);

SyntaxNodeOrToken edit := Insert(node, match, ast, k) | Update(node, match, ast) | Move(node, k, match, match) | Delete(node, match);

MatchResult match := C(node, kind, children) | Tree(kindRef) | Parent(node, kindRef, k);
MatchResult kindRef := Variable(node, kind, k) | Literal(node, tree);

IEnumerable<MatchResult> children := SC(match) | CList(match, children);

SyntaxNodeOrToken ast:= Node(kind, childrenNodes) | Const(tree) | Ref(node, match);
IEnumerable<SyntaxNodeOrToken> childrenNodes := SN(ast) | NList(ast, childrenNodes);

@feature[Score=KindScore] SyntaxKind kind;                     @input SyntaxNodeOrToken n;
@feature[Score=KDScore] string id;                             @feature[Score=KScore] int k;
@feature[Score=NodeScore] SyntaxNodeOrToken tree;
